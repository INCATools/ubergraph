#+ summary: Find replaced by term in obsolete term
#+ description: Enter an obsolete term and return the term that replaces it 

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX replaced_by: <http://purl.obolibrary.org/obo/IAO_0100001>
PREFIX oboinowl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?obsolete_class ?replaced_by_iri
WHERE {
  {
    ?_obsolete_iri rdf:type owl:Class; 
      owl:deprecated "true"^^xsd:boolean; 
      replaced_by: ?replaced_by .
  }
  UNION
  {
    ?replaced_by rdf:type owl:Class;
      oboinowl:hasAlternativeId ?_obsolete_iri .
    FILTER(isIRI(?replaced_by))
  }
  BIND(?_obsolete_iri AS ?obsolete_class)
  BIND (IF(isLiteral(?replaced_by), IRI(CONCAT("http://purl.obolibrary.org/obo/", STRBEFORE(?replaced_by, ":"), "_", STRAFTER(?replaced_by, ":"))), ?replaced_by) AS ?replaced_by_iri)
}